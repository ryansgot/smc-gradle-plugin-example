buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'com.fsryan.gradle.smc:smc:0.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'smc'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.fsryan.example.smcgradleplugin.android"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavorDimensions 'dimen1'

        flavor1 {
            dimension 'dimen1'
        }
        flavor2 {
            dimension 'dimen1'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/kotlin'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation fileTree(dir: new File(rootProject.projectDir, 'libs'), include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"

    implementation 'com.android.support:appcompat-v7:27.1.1'
}

smc {
    // without an smcUri, the plugin will attempt to download version 6.6.0 from the download site
    // if successful, it will extract the Smc.jar and/or the statemap.jar from the downloaded zip file
    // to demonstrate downloading, run ./gradlew generateStateMachineSources -PdownloadSmc -PdownloadStatemap
    smcUri = project.hasProperty('downloadSmc') ? null : 'file://' + rootProject.projectDir.absolutePath + File.separator + 'bin' + File.separator + 'Smc.jar'
    statemapJarUri = project.hasProperty('downloadStatemap') ? null : 'file://' + rootProject.projectDir.absolutePath + File.separator + 'libs' + File.separator + 'statemap.jar'

    libsDirectory = "libs"  // <-- the default, you should have dependencies { implementation fileTree(dir: 'libs', include: ['*.jar']) }
    smSrcDir = "sm"         // <-- the default, you should have src/main/sm and then use a directory structure that matches your java package structure
    graphVizLevel = 2       // <-- put all info in generated graphviz diagram (there three levels, 0, 1, and 2 and -1 means "don't generate")
    outputHtmlTable = true  // <-- generate an HTML table representation
}